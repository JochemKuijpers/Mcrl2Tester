% (a) Any wafer passing through In must be premeasured before moving to Meas
[true* . move_r1(Tray, In) . (!premeasure)* . move_r2(In, Meas)]false &&

% (b) Any wafer passing through Meas from In must be measured before swapping
[true* . move_r2(In, Meas) . (!measure)* . swap]false &&
[true* . move_r2(In, Meas) . (!swap)* . move_r3(Meas, Out)]false &&

% (c) Any wafer passing from In through Meas and to Proj must be projected before swapping again
% [true* . swap . (!project)* . swap . (!swap)* . move_r3(Meas, Out)]false
[true* . projection_done . (!swap)* . move_r3(Meas, Out)] false &&

% (d) Eventually we will move every processed wafer back into the tray
[true*] forall t: ChuckType . forall n: Nat . [lot_info(n,t)]
nu X(nIn: Nat = n, nOut: Nat = n) . (
	forall newWafers: Nat, newLot: ChuckType .
	[move_r1(Tray, In)] X(nIn = nIn -1, nOut) &&
	[move_r1(Out, Tray)] X(nIn, nOut = nOut-1)&&
	[lot_info(newWafers, newLot)] val(nIn != 0) => false &&
	[lot_info(newWafers, newLot)] val(nOut != 0) => false &&
	[!(move_r1(Tray, In) || move_r1(Out, Tray) || exists m:Nat, q:ChuckType . lot_info(m,q))] X(nIn, nOut)
)
